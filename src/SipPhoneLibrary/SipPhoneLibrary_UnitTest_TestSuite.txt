*** Settings ***
Documentation     This test suite also serves as a unit test for the SipPhoneLibrary.
Library           String
Library           ../SipPhoneLibrary/
Library           Collections

*** Variables ***

*** Test Cases ***
Example Test Case 1
    Setup Phone    ${EXT1}    ${EXT1_IP}    port=${EXT1_PORT}    timeout=5 seconds
    Setup Phone    ${EXT2}    ${EXT2_IP}    port=${EXT2_PORT}    timeout=5 seconds
    ${model1}=    Get Phone Model    ${EXT1}
    Log    ${model1}
    ${model2}=    Get Phone Model    ${EXT2}
    Log    ${model2}
    ${mac1}=    Get Mac    ${EXT1}
    Log    ${mac1}
    ${mac2}=    Get Mac    ${EXT2}
    Log    ${mac2}
    ${reg}=    Get Registered Lines    ${EXT1}
    Dictionary Should Contain Value    ${reg}    ${EXT1}
    ${reg}=    Get Registered Lines    ${EXT2}
    Dictionary Should Contain Value    ${reg}    ${EXT2}
    Dial Number    ${EXT1}    ${EXT2}
    Wait Until Keyword Succeeds    3 seconds    500 ms    Expect Ringback    ${EXT1}
    Accept Call    ${EXT2}
    Sleep    1
    Expect Connected    ${EXT1}
    Expect Connected    ${EXT2}
    End Call    ${EXT2}

*** Keywords ***
Dial Number
    [Arguments]    ${ext}    ${number}
    [Documentation]    Take the specified phone offhook (by pressing the headset key), dial the specified number.
    ${digits}=    Split String To Characters    ${number}
    : FOR    ${digit}    IN    @{digits}
    \    Press Digit    ${ext}    ${digit}
    Press Headset Key    ${ext}

Accept Call
    [Arguments]    ${ext}
    [Documentation]    Answer a ringing phone (by pressing the headset key).
    Press Headset Key    ${ext}

End Call
    [Arguments]    ${ext}
    [Documentation]    End a call (by pressing the headset key).
    Press Headset Key    ${ext}
